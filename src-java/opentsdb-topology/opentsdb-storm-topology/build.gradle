plugins {
    id 'io.freefair.aspectj.post-compile-weaving'
    id 'com.github.johnrengelman.shadow'
}

compileJava.ajc.options.compilerArgs += '-showWeaveInfo'
compileJava.ajc.options.compilerArgs += '-verbose'

description = 'OpenTSDB Storm Topology'
dependencies {
    implementation project(':blue-green')
    implementation project(':base-storm-topology')
    runtimeOnly project(path: ':base-storm-topology', configuration: 'releaseArtifacts')
    testImplementation project(path: ':base-storm-topology', configuration: 'testArtifacts')
    implementation project(':kilda-reporting')
    testImplementation project(path: ':kilda-persistence-tinkerpop', configuration: 'testArtifacts')

    runtimeOnly 'org.yaml:snakeyaml:1.30'
    runtimeOnly 'commons-logging:commons-logging:1.2'
    implementation('org.apache.storm:storm-opentsdb:1.2.1') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        // This conflicts with jcl-over-slf4j
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    compileOnly('org.apache.storm:storm-core')
    testImplementation('org.apache.storm:storm-core') {
        exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
    }

    implementation 'org.hibernate.validator:hibernate-validator'

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.vintage:junit-vintage-engine'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.hamcrest:hamcrest-library'
    testRuntimeOnly 'org.glassfish:javax.el'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl'
    testImplementation('org.mock-server:mockserver-netty:5.11.2') {
        exclude group: 'ch.qos.logback'
    }
    testRuntimeOnly 'javax.servlet:javax.servlet-api'

    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:2.26'
    implementation 'org.glassfish.jersey.core:jersey-client:2.26'
    implementation('org.glassfish.jersey.inject:jersey-hk2:2.26') {
        // These classes are bundled in org.glassfish.hk2.external:javax.inject
        exclude group: 'javax.inject', module: 'javax.inject'
    }
    implementation 'org.glassfish.jersey.core:jersey-common:2.26'
    implementation ('org.glassfish.jersey.connectors:jersey-apache-connector:2.26') {
        // This conflicts with jcl-over-slf4j
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding'
    testAnnotationProcessor 'org.projectlombok:lombok-mapstruct-binding'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.openkilda.wfm.topology.opentsdb.OpenTsdbTopology'
    }
    classifier = 'original'
}

shadowJar {
    classifier = ''
    mergeServiceFiles()
}

artifacts {
    archives shadowJar
}
