plugins {
    id "io.freefair.aspectj.post-compile-weaving"
    id "com.github.johnrengelman.shadow"
}

compileJava.ajc.options.compilerArgs += "-showWeaveInfo"
compileJava.ajc.options.compilerArgs += "-verbose"

description = "Isl Latency Storm Topology"
dependencies {
    implementation(project(":base-storm-topology"))
    implementation(project(":blue-green"))
    implementation(project(":isllatency-messaging"))
    implementation(project(":network-messaging"))
    implementation(project(":floodlight-api"))
    implementation("org.yaml:snakeyaml:2.2")

    aspect(project(":kilda-persistence-api"))
    runtimeOnly(project(":kilda-persistence-orientdb"))
    runtimeOnly(project(":kilda-persistence-hibernate"))
    // runtimeClasspath because "releaseArtifacts" shouldn"t go for tests.
    runtimeClasspath(project(path: ":base-storm-topology", configuration: "releaseArtifacts"))
    compileOnly("org.apache.storm:storm-core"){
        exclude(group: "org.yaml", module: "snakeyaml")
    }

    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("org.mockito:mockito-junit-jupiter")
    testImplementation("org.hamcrest:hamcrest-library")
    testImplementation(project(path: ":base-storm-topology", configuration: "testArtifacts"))
    testImplementation("org.apache.storm:storm-core")
    testImplementation(project(path: ":kilda-persistence-api", configuration: "testArtifacts"))
    testImplementation(project(path: ":kilda-persistence-tinkerpop", configuration: "testArtifacts"))

    testRuntimeOnly("org.glassfish:jakarta.el")
    testRuntimeOnly("org.apache.curator:curator-test:5.5.0")
    testRuntimeOnly("io.dropwizard.metrics:metrics-core:3.2.6!!")
    testRuntimeOnly("org.apache.logging.log4j:log4j-slf4j-impl")

    compileOnly("org.projectlombok:lombok")
    testCompileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok-mapstruct-binding")
    testAnnotationProcessor("org.projectlombok:lombok-mapstruct-binding")
}

jar {
    manifest {
        attributes 'Main-Class': 'org.openkilda.wfm.topology.isllatency.IslLatencyTopology'
    }

    archiveClassifier = "original"
}

shadowJar {
    archiveClassifier = ""
    mergeServiceFiles()
    zip64 true
}

artifacts {
    archives shadowJar
}

test {
    useJUnitPlatform()
}
