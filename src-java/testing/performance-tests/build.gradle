plugins {
    id 'groovy'
}

description = 'Performance-Tests'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    implementation(platform('org.springframework:spring-framework-bom:5.2.13.RELEASE'))

    implementation project(':test-library')
    implementation project(':functional-tests')
    
    implementation('com.fasterxml.jackson.core:jackson-databind')
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'

    implementation 'org.codehaus.groovy:groovy-all:3.0.6'

    implementation 'org.codehaus.gpars:gpars:1.2.1'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl'
    implementation 'org.apache.logging.log4j:log4j-api'
    implementation 'org.apache.logging.log4j:log4j-core'

    implementation 'org.hibernate.validator:hibernate-validator'
    implementation 'org.glassfish:jakarta.el'
    
    implementation 'org.spockframework:spock-spring:2.1-groovy-3.0-SNAPSHOT'
    implementation 'org.spockframework:spock-core:2.1-groovy-3.0-SNAPSHOT'
    implementation 'net.jodah:failsafe'
}

test {
    exclude '**/*'
}

task performanceTest(type: Test, dependsOn: 'compileGroovy') {
    description = 'Runs performance tests.'
    failFast = true
    include '**/performancetests/**'
    systemProperty 'tags', System.getProperty('tags')
    Properties properties = new Properties()
    File propertiesFile = file("kilda.properties.example")
    propertiesFile.withInputStream {
        properties.load(it)
    }
    properties.keys().each { k ->
        if (System.getProperty(k)) {
            systemProperty k, System.getProperty(k)
        }
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    outputs.upToDateWhen { false } //never cache results of functional tests
}