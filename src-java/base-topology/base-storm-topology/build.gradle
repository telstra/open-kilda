plugins {
    id 'java-library'
    id 'io.freefair.aspectj.post-compile-weaving'
}

compileJava.ajc.options.compilerArgs += '-showWeaveInfo'
compileJava.ajc.options.compilerArgs += '-verbose'

description = 'Base Storm Topology'
dependencies {
    api project(':base-messaging')
    api project(':kilda-configuration')
    api project(':kilda-persistence-tinkerpop')
    aspect project(':kilda-persistence-api')
    implementation project(':blue-green')
    implementation project(':kilda-pce')
    implementation project(':kilda-reporting')
    implementation project(':floodlight-api')
    implementation project(':grpc-api')
    testImplementation project(path: ':kilda-persistence-api', configuration: 'testArtifacts')
    testImplementation project(path: ':kilda-persistence-tinkerpop', configuration: 'testArtifacts')
    testImplementation project(':kilda-utils:stubs')

    api 'org.apache.storm:storm-kafka-client'

    compileOnly 'org.apache.storm:storm-core'
    testImplementation('org.apache.storm:storm-core') {
        exclude(group: 'org.slf4j', module: 'log4j-over-slf4j')
    }

    api('org.squirrelframework:squirrel-foundation') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'
    }

    api 'com.google.guava:guava'
    api 'org.apache.commons:commons-lang3'
    api 'commons-lang:commons-lang'
    api 'org.apache.commons:commons-collections4'
    api 'com.fasterxml.jackson.core:jackson-annotations'
    api 'com.fasterxml.jackson.core:jackson-databind'
    api 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    api 'args4j:args4j'
    implementation 'com.google.code.gson:gson'
    api 'com.fasterxml.uuid:java-uuid-generator'
    api 'net.jodah:failsafe'
    api 'org.hibernate.validator:hibernate-validator'
    runtimeOnly 'org.glassfish:javax.el'

    implementation 'org.aspectj:aspectjrt'
    implementation 'org.mapstruct:mapstruct-jdk8'
    implementation 'org.mapstruct:mapstruct-processor'
    annotationProcessor 'org.mapstruct:mapstruct-processor'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor'

    api('org.apache.kafka:kafka-clients') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'
    }
    api('org.apache.kafka:kafka_2.11') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'
    }

    testImplementation 'junit:junit'
    testImplementation 'pl.pragmatists:JUnitParams'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.hamcrest:hamcrest-library'
    testImplementation 'org.apache.curator:curator-test'
    testImplementation 'org.clojure:clojure'
    testRuntimeOnly 'org.hibernate.validator:hibernate-validator'
    testRuntimeOnly 'org.glassfish:javax.el'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl'

    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

sourceSets {
    release {
        resources {
            srcDir 'src/release/resources'
        }
    }
}

configurations {
    stormFluxArtifacts {
        dependencies {
            stormFluxArtifacts('org.apache.storm:flux-core:1.2.1') {
                exclude(group: 'commons-cli', module: 'commons-cli')
            }
        }
        outgoing {
            capability("${project.group}:${project.name}-flux:${project.version}")
            // Below are dependencies bundled in org.apache.storm:flux-core:1.1.0
            capability('commons-cli:commons-cli:1.2')
            capability('commons-codec:commons-codec:1.6')
            capability('commons-logging:commons-logging:1.1.3')
            capability('org.apache.httpcomponents:httpclient:4.3.3')
            capability('org.apache.httpcomponents:httpcore:4.4.1')
            capability('org.apache.storm:flux-wrappers:1.1.0')
            capability('org.yaml:snakeyaml:1.11')
        }
    }

    testArtifacts
    releaseArtifacts
}

task testJar(type: Jar) {
    dependsOn compileTestJava
    classifier 'test'
    from sourceSets.test.output
}

task releaseJar(type: Jar) {
    dependsOn processResources
    classifier 'release'
    from sourceSets.release.output
}

artifacts {
    testArtifacts testJar
    releaseArtifacts releaseJar
}

buildAndCopyArtifacts {
    from("${project.buildDir}/resources/release/topology.properties") { into "${project.name}/resources" }
}
