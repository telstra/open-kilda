buildscript {
    repositories {
        maven { url "https://jitpack.io" }
    }
}

plugins {
    id "java-library"
    id "io.freefair.aspectj.post-compile-weaving"
}

compileJava.ajc.options.compilerArgs += "-showWeaveInfo"
compileJava.ajc.options.compilerArgs += "-verbose"
compileJava.options.compilerArgs += "-Amapstruct.unmappedTargetPolicy=ERROR"

description = "Base Storm Topology"
dependencies {
    api project(":base-messaging")
    api project(":kilda-configuration")
    api project(":kilda-persistence-api")
    aspect project(":kilda-persistence-api")
    api project(":blue-green")
    implementation project(":kilda-pce")
    implementation project(":kilda-reporting")
    implementation project(":floodlight-api")
    implementation project(":grpc-api")
    testImplementation project(path: ":kilda-persistence-api", configuration: "testArtifacts")
    testImplementation project(path: ":kilda-persistence-tinkerpop", configuration: "testArtifacts")
    testImplementation project(":kilda-utils:stubs")

    api "org.apache.storm:storm-kafka-client:2.6.1"

    /**
     * This dependency must be compileOnly to prevent storm to be packed via transitive dependencies into the shadowJar,
     * which is deployed to Storm later. This will cause a duplicate configuration error, because storm jar is already
     * provided in the storm cluster. ShadowJar supports excludes, but it only excludes dependencies from the current
     * project.
     */
    compileOnly("org.apache.storm:storm-core") {
        exclude(group: "org.yaml", module: "snakeyaml")
    }
    implementation("org.yaml:snakeyaml:2.2")
    testImplementation("org.apache.storm:storm-core") {
        exclude group: "org.slf4j", module: "log4j-over-slf4j"
    }

    implementation("org.rocksdb:rocksdbjni:8.10.0") { because("required for Storm integration tests") }
    implementation("org.apache.storm:flux-core:2.6.1")

    api("org.squirrelframework:squirrel-foundation") {
        exclude group: "org.slf4j", module: "slf4j-log4j12"
        exclude group: "log4j", module: "log4j"
    }

    api("com.google.guava:guava")
    api("org.apache.commons:commons-lang3")
    api("org.apache.commons:commons-collections4")
    api( "com.fasterxml.jackson.core:jackson-annotations")
    api("com.fasterxml.jackson.core:jackson-databind")
    api("com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider")
    api("args4j:args4j")
    api("com.fasterxml.uuid:java-uuid-generator")
    api("net.jodah:failsafe")
    api("org.hibernate.validator:hibernate-validator")
    runtimeOnly "org.glassfish:javax.el"

    api("org.aspectj:aspectjrt")
    implementation("org.mapstruct:mapstruct")
    implementation("org.mapstruct:mapstruct-processor")
    annotationProcessor("org.mapstruct:mapstruct-processor")
    testAnnotationProcessor("org.mapstruct:mapstruct-processor")

    api("org.apache.kafka:kafka-clients:3.6.1") {
        exclude group: "org.slf4j", module: "slf4j-log4j12"
        exclude group: "log4j", module: "log4j"
    }
    api("org.apache.kafka:kafka_2.12:3.6.1") {
        exclude group: "org.slf4j", module: "slf4j-log4j12"
        exclude group: "log4j", module: "log4j"
    }

    api("io.micrometer:micrometer-core:1.10.5")

    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testImplementation("org.mockito:mockito-junit-jupiter")
    testImplementation("org.hamcrest:hamcrest-library")
    testImplementation("org.apache.curator:curator-test")
    testRuntimeOnly("org.hibernate.validator:hibernate-validator")
    testRuntimeOnly("org.glassfish:javax.el")
    testRuntimeOnly("org.apache.logging.log4j:log4j-slf4j-impl")

    compileOnly("org.projectlombok:lombok")
    testCompileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok-mapstruct-binding")
    testAnnotationProcessor("org.projectlombok:lombok-mapstruct-binding")
}

sourceSets {
    releaseResources {
        resources {
            srcDir("src/release/resources")
        }
    }
}

configurations {
    testArtifacts
    releaseArtifacts
}

tasks.register("testJar", Jar) {
    dependsOn compileTestJava
    classifier "test"
    from sourceSets.test.output
}

tasks.register("releaseJar", Jar) {
    dependsOn processResources
    classifier "release"
    from sourceSets.releaseResources.output
}

artifacts {
    testArtifacts testJar
    releaseArtifacts releaseJar
}

buildAndCopyArtifacts {
    from("${project.buildDir}/resources/releaseResources/topology.properties") { into "${project.name}/resources" }
}

test {
    useJUnitPlatform()
}
