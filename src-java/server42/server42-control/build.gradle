plugins {
    id 'org.springframework.boot' version '2.2.13.RELEASE'
}

configurations {
    // This conflicts with spring-boot-starter-log4j2
    implementation.exclude module: 'spring-boot-starter-logging'
}

description = 'server42-control'
dependencies {
    implementation(platform('org.springframework:spring-framework-bom:5.2.19.RELEASE'))
    implementation(platform('org.springframework.boot:spring-boot-dependencies:2.2.13.RELEASE'))

    implementation project(':kilda-model')
    implementation project(':blue-green')

    implementation project(':server42-control-messaging')
    implementation project(':server42-messaging')

    implementation 'com.google.protobuf:protobuf-java:3.21.2'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'joda-time:joda-time:2.10.13'
    implementation 'io.micrometer:micrometer-core:1.9.2'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.9.2'
    implementation 'org.zeromq:jeromq:0.4.3'

    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-joda'

    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter')
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation('org.springframework.kafka:spring-kafka') {
        version {
            strictly '2.3.13.RELEASE'
        }
        // This conflicts with kafka_2.11
        exclude module:'kafka_2.12'
    }
    testImplementation('org.springframework.kafka:spring-kafka-test') {
        version {
            strictly '2.3.13.RELEASE'
        }
        // This conflicts with kafka_2.11
        exclude module:'kafka_2.12'
    }
    implementation 'org.apache.kafka:kafka-clients'
    implementation('org.apache.kafka:kafka_2.11') {
        version {
            strictly '2.3.1'
        }
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'
    }

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding'
}

bootJar {
    mainClassName = 'org.openkilda.server42.control.ControlApplication'
    archiveFileName = "${archivesBaseName}.${archiveExtension.get()}"
}

bootJar.dependsOn generateVersionTxt
