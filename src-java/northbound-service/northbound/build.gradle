plugins {
    id 'org.springframework.boot' version '2.2.13.RELEASE'
}

configurations {
    compile.exclude module: 'spring-boot-starter-logging'
}

description = 'Northbound Service'
dependencies {
    implementation(platform('org.springframework:spring-framework-bom:5.2.13.RELEASE'))
    implementation(platform('org.springframework.boot:spring-boot-dependencies:2.2.13.RELEASE'))

    implementation project(':northbound-api')
    implementation project(':flowhs-messaging')
    implementation project(':nbworker-messaging')
    implementation project(':ping-messaging')
    implementation project(':floodlight-api')
    implementation project(':kilda-persistence-api')
    implementation project(':blue-green')
    runtimeClasspath project(':kilda-persistence-orientdb')
    runtimeClasspath project(':kilda-persistence-mixture')
    implementation project(':kilda-configuration')
    testImplementation project(':kilda-utils:stubs')

    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter')
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'org.springframework.security:spring-security-config'
    implementation 'org.springframework.security:spring-security-web'
    implementation 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation ('org.springframework.kafka:spring-kafka') {
        version {
            strictly '2.3.13.RELEASE'
        }
        // This conflicts with kafka_2.11
        exclude module:'kafka_2.12'
        exclude group: 'org.apache.kafka', module: 'kafka-clients'
    }
    implementation 'org.springframework:spring-webmvc'
    implementation('org.apache.kafka:kafka-clients') {
        version {
            strictly '2.3.1'
        }
    }
    implementation('org.apache.kafka:kafka_2.11') {
        version {
            strictly '2.3.1'
        }
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'
    }
    implementation 'org.apache.logging.log4j:log4j-api'
    implementation 'org.apache.logging.log4j:log4j-core'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'io.springfox:springfox-swagger2'
    implementation 'io.springfox:springfox-swagger-ui'

    implementation('com.fasterxml.jackson.core:jackson-databind')
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')

    implementation 'com.google.guava:guava'
    implementation 'org.apache.commons:commons-collections4'
    implementation 'com.sabre.oss.conf4j:conf4j-spring-boot'

    implementation 'org.mapstruct:mapstruct'
    implementation 'org.mapstruct:mapstruct-processor'
    annotationProcessor 'org.mapstruct:mapstruct-processor'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude module: 'commons-logging'
    }
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.vintage:junit-vintage-engine'
    testImplementation project(path: ':kilda-persistence-tinkerpop', configuration: 'testArtifacts')
}

bootJar {
    mainClassName = 'org.openkilda.northbound.Application'
    archiveFileName = "${archivesBaseName}.${archiveExtension.get()}"
}

bootJar.dependsOn generateVersionTxt
