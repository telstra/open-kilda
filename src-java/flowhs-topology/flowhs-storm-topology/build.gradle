plugins {
    id "io.freefair.aspectj.post-compile-weaving"
    id "com.github.johnrengelman.shadow"
}

compileJava.ajc.options.compilerArgs += "-showWeaveInfo"
compileJava.ajc.options.compilerArgs += "-verbose"
compileJava.options.compilerArgs += "-Amapstruct.unmappedTargetPolicy=ERROR"

description = "Flow H&S Storm Topology"
dependencies {
    implementation(project(":blue-green"))
    implementation(project(":flowhs-messaging"))
    implementation(project(":flowmonitoring-messaging"))
    implementation(project(":ping-messaging"))
    implementation(project(":reroute-messaging"))
    implementation(project(":server42-control-messaging"))
    implementation(project(":base-storm-topology"))
    implementation(project(":stats-messaging"))
    implementation(project(":kilda-pce"))
    implementation(project(":rule-manager-api"))
    implementation(project(":rule-manager-implementation"))
    implementation(project(":kilda-reporting"))
    implementation(project(":floodlight-api"))
    implementation("org.yaml:snakeyaml:2.2")

    aspect(project(":base-storm-topology"))
    runtimeOnly(project(":kilda-persistence-orientdb"))
    runtimeOnly(project(":kilda-persistence-hibernate"))
    runtimeClasspath(project(path: ":base-storm-topology", configuration: "releaseArtifacts")) because("'releaseArtifacts' shouldn't go for tests.")

    compileOnly("org.apache.storm:storm-core") {
        exclude(group: "org.yaml", module: "snakeyaml")
    }

    testImplementation(project(path: ":base-storm-topology", configuration: "testArtifacts"))
    testImplementation(project(path: ":kilda-persistence-api", configuration: "testArtifacts"))
    testImplementation(project(path: ":kilda-persistence-tinkerpop", configuration: "testArtifacts"))

    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testImplementation("org.mockito:mockito-junit-jupiter")
    testImplementation("org.hamcrest:hamcrest-library")
    testImplementation("org.apache.storm:storm-core")

    implementation("org.mapstruct:mapstruct")
    implementation("org.mapstruct:mapstruct-processor")
    annotationProcessor("org.mapstruct:mapstruct-processor")
    testAnnotationProcessor("org.mapstruct:mapstruct-processor")

    compileOnly("org.projectlombok:lombok")
    testCompileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok-mapstruct-binding")
    testAnnotationProcessor("org.projectlombok:lombok-mapstruct-binding")
}

jar {
    manifest {
        attributes 'Main-Class': 'org.openkilda.wfm.topology.flowhs.FlowHsTopology'
    }

    archiveClassifier = "original"
}

shadowJar {
    archiveClassifier = ""
    mergeServiceFiles()
    zip64 true
}

artifacts {
    archives shadowJar
}

test {
    useJUnitPlatform()
}
