plugins {
    id 'java-base'
    id 'org.ajoberstar.grgit' version '4.0.1' apply false
}

group = 'org.projectfloodlight'
def versionTag = '1.3-kilda-1'

import org.ajoberstar.grgit.Grgit

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

def extensions = project.getExtensions()
def toolchain = extensions.getByType(JavaPluginExtension.class).toolchain
def service = extensions.getByType(JavaToolchainService.class)
def jdk8Home = service.launcherFor(toolchain).get().metadata.installationPath.asFile.absolutePath

configurations {
    'default'
    openflowj
}

dependencies {
    openflowj 'org.projectfloodlight:openflowj'
}

task cloneFloodlightRepo {
    def repo = file('floodlight')
    outputs.dir(repo)

    if (repo.exists()) {
        try {
            def grgit = Grgit.open(currentDir: repo.absolutePath)
            grgit.fetch()
            grgit.checkout(branch: versionTag)
            grgit.close()
        } catch (Exception e) {
            logger.warn("Failed to fetch floodlight from git: {}", e.getMessage())
        }
    } else {
        def repoUri = 'https://github.com/kilda/floodlight.git'
        if (project.hasProperty("floodlightGitRepo")) {
            repoUri = project.property("floodlightGitRepo")
        }
        def grgit = Grgit.clone(dir: repo.absolutePath, uri: repoUri, refToCheckout: versionTag)
        grgit.close()
    }
}

task cleanSources(type: Exec) {
    workingDir 'floodlight'
    environment 'JAVA_HOME', jdk8Home
    commandLine 'mvn', '-B', 'clean'
}
clean.dependsOn(cleanSources)

task compileMavenProject(type: Exec, dependsOn: [configurations.openflowj, cloneFloodlightRepo]) {
    inputs.file('floodlight/pom.xml')
    inputs.dir('floodlight/src')
    inputs.dir('floodlight/lib')
    outputs.file("floodlight/target/floodlight-${versionTag}.jar")
    outputs.file('floodlight/target/floodlight.jar')

    workingDir 'floodlight'
    environment 'JAVA_HOME', jdk8Home
    commandLine 'mvn', '-B', 'clean', 'install', '-DskipTests'
}

task copyJar(type: Copy, dependsOn: compileMavenProject) {
    ext.destFile = file("$buildDir/libs/floodlight-${versionTag}.jar")

    from(compileMavenProject)
    into "$buildDir/libs"
}

artifacts {
    'default' file: copyJar.destFile, builtBy: copyJar
}
